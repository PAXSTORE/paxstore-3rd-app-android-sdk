apply plugin: 'maven-publish'
apply plugin: 'signing'


ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["developerName"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
}


def PUBLISH_GROUP_ID = 'com.whatspos.sdk' //这里可以不是直接申请时候的groupId只要开头是就可以

def PUBLISH_ARTIFACT_ID = "paxstore-3rd-app-android-sdk"

def PUBLISH_VERSION = project.version //这个是直接获取的库gradle里配置好的版本号，不用到处修改版本号，只需要维护一份就可以。

// 当你发布的是一个纯java的依赖，那么就需要把下面的方法打开因为纯java的依赖，maven那边要求提供完整的javadoc，
// 但是aar不需要提供，也无法提供，因为下面的task javadocJar命令无法被识别
//task javadocJar(type: Jar) {
//    classifier = 'javadoc'
//    from javadoc
//}
//
//task sourcesJar(type: Jar) {
//    classifier = 'sources'
//    from sourceSets.main.allSource
//}
//
//artifacts {
//    archives javadocJar, sourcesJar
//}
android {
    publishing {
        singleVariant('release') {
            withJavadocJar()
            withSourcesJar()
        }
    }
}


publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release
            }

            groupId = PUBLISH_GROUP_ID
            artifactId = PUBLISH_ARTIFACT_ID
            version = PUBLISH_VERSION


            pom {
                name = 'paxstore-3rd-app-android-sdk'
                description = 'PAXSTORE 3rd App Android SDK provides simple and easy-to-use service interfaces for third party developers to develop android apps on PAXSTORE.'
                url = 'https://github.com/PAXSTORE/paxstore-3rd-app-android-sdk'

                scm {
                    //修改成你的Git地址：
                    connection = 'scm:git:github.com/PAXSTORE/paxstore-3rd-app-android-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com/PAXSTORE/paxstore-3rd-app-android-sdk.git'
                    //分支地址：
                    url = 'https://github.com/PAXSTORE/paxstore-3rd-app-android-sdk/tree/master'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers { // 改成你自己的信息
                    developer {
                        name = 'Derek'
                        email = 'paxstore-support@paxsz.com'
                    }
                }
            }

        }
    }
    repositories {
        if (project.version.endsWith('SNAPSHOT')) {
            // snapshot can be uploaded to cloud directly
            // you need to add below to your top level build.gradle to get this dependency
            // maven { url "https://central.sonatype.com/repository/maven-snapshots/" }
            maven {
                url = "https://central.sonatype.com/repository/maven-snapshots/"
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        } else {
            // publish release version, files needed will be generated to build/staging-deploy
            // zip com directory under staging-deploy file, and go to maven portal to release
            maven {
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }

}

//签名
signing {
    sign publishing.publications.release
}
